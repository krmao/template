apply plugin: 'com.android.application'
apply plugin: "kotlin-android"
apply plugin: 'kotlin-android-extensions'
// apply from: "$settingsDir/gradle/project_config.gradle"
//apply plugin: 'walle'
//
//walle {
//    apkOutputFolder = new File("${buildDir}/outputs/channels")// 指定渠道包的输出路径
//    apkFileNameFormat = '${appName}-${channel}-${buildType}-v${versionName}-${versionCode}-${buildTime}.apk'
//    channelFile = new File("${projectDir}/channels.txt")// 渠道配置文件
//}

//上传bugly符号表配置
/*apply plugin: 'bugly'
bugly {
    appId = ''
    appKey = ''
}*/

android {
    compileSdkVersion = rootProject.ext.compileSdkVersion
    buildToolsVersion = rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.smart.template"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        vectorDrawables.useSupportLibrary = true
        // Gradle Plugin 2.0以上,AppCompat23.2以上,Android 5.0之前使用Vector

        /**
         * 分包，指定某个类在main dex
         * https://juejin.im/post/5d95f4a4f265da5b8f10714b
         */
        multiDexEnabled true
        multiDexKeepFile file('maindexlist.txt') // 指定哪些类要放到main dex
        multiDexKeepProguard file('multiDexKeep.pro') // 打包到main dex的这些类的混淆规制，没特殊需求就给个空文件

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        ndk {
            abiFilters 'armeabi-v7a', 'x86', 'x86_64', 'arm64-v8a'
        }

        println "\ndebug=${rootProject.ext.debug}, \n" +
                "VERSION_RN=${rootProject.ext.versionRN}, \n" +
                "MAP_ENABLE=${gradle.ext.modules.map.enable}, \n" +
                "ENABLE_MAP_NAMES=${gradle.ext.modules.map.enableMapNames}, \n" +
                "BAIDU_MAP_KEY=${gradle.ext.modules.map.baiduKey}, \n" +
                "GAODE_MAP_KEY=${gradle.ext.modules.map.gaodeKey} \n"

        manifestPlaceholders = [
                'DEBUG'     : rootProject.ext.debug,
                'VERSION_RN': rootProject.ext.versionRN
        ]

        if (gradle.ext.modules.map.enable == true) {
            manifestPlaceholders.put('ENABLE_MAP_NAMES', gradle.ext.modules.map.enableMapNames)
            if (gradle.ext.modules.map.enableMapNames.contains("baidu")) {
                manifestPlaceholders.put('BAIDU_MAP_KEY', gradle.ext.modules.map.baiduKey)
            } else {
                manifestPlaceholders.put('BAIDU_MAP_KEY', '')
            }
            if (gradle.ext.modules.map.enableMapNames.contains("gaode")) {
                manifestPlaceholders.put('GAODE_MAP_KEY', gradle.ext.modules.map.gaodeKey)
            } else {
                manifestPlaceholders.put('GAODE_MAP_KEY', '')
            }
        }

    }

    useLibrary 'org.apache.http.legacy'

    resourcePrefix ""

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    signingConfigs {

        /**
         *  MD5:  48:96:96:D2:39:CA:C7:0B:1F:10:F4:4D:0D:23:32:09
         *  SHA1: 31:FB:96:47:CB:48:76:99:B3:FE:00:BA:0E:AB:71:1D:EE:DC:58:77
         *  SHA256: CE:AB:57:F5:1F:BB:71:5F:7C:C2:4F:89:CF:81:01:75:56:02:C1:1E:12:72:E3:44:1B:4A:EA:67:9E:F9:00:B1
         */
        debug {
            keyAlias 'debug'
            keyPassword '123456'
            storeFile file('debug.keystore')
            storePassword '123456'
            v2SigningEnabled true
        }

        release {
            keyAlias 'debug'
            keyPassword '123456'
            storeFile file('debug.keystore')
            storePassword '123456'
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
            // proguardFiles "$rootDir/readme/scripts/proguard_base.pro", "proguard-rules.pro"
            // testProguardFiles "$rootDir/readme/scripts/proguard_base.pro", "proguard-rules.pro"
        }

        release {
            debuggable false
            jniDebuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            signingConfig signingConfigs.release
            proguardFiles "$rootDir/readme/scripts/proguard_base.pro", "proguard-rules.pro"
            testProguardFiles "$rootDir/readme/scripts/proguard_base.pro", "proguard-rules.pro"
        }
    }


    testOptions {
        unitTests.returnDefaultValues = false
        resultsDir = "${buildDir}/test-reports"

        // Always show the result of every unit test, even if it passes.
        unitTests.all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/NOTICE.TXT'
        exclude 'META-INF/LICENSE.TXT'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/rxjava.properties'
    }

    dexOptions {
        javaMaxHeapSize "4g"
        preDexLibraries = false
    }

    lintOptions {
        fatal 'UnusedResources'
        warning 'ResourceType'
        checkReleaseBuilds false
        abortOnError false
        lintConfig file("$rootDir/lint.xml")
        showAll true
        absolutePaths true
        textReport true
        textOutput file("$buildDir/outputs/lint/${project.name}_lint-report.txt")
        xmlReport true
        xmlOutput file("$buildDir/outputs/lint/${project.name}_lint-report.xml")
        htmlReport true
        htmlOutput file("$buildDir/outputs/lint/${project.name}_lint-report.html")
    }

    configurations.all {
        resolutionStrategy {
            force "com.google.code.findbugs:jsr305:1.3.9"
            force "com.squareup.okhttp3:okhttp:3.12.1"

            force "androidx.fragment:fragment-ktx:$rootProject.ext.androidXFragmentVersion"
            force "androidx.fragment:fragment:$rootProject.ext.androidXFragmentVersion"
            force "androidx.viewpager:viewpager:$rootProject.ext.androidXVersion"
            force "androidx.legacy:legacy-support-v4:$rootProject.ext.androidXVersion"
            force "androidx.appcompat:appcompat:$rootProject.ext.androidXAppCompatVersion"
            force "androidx.annotation:annotation:$rootProject.ext.androidXAnnotationVersion"
            force "androidx.constraintlayout:constraintlayout:$rootProject.ext.androidXConstraintlayoutVersion"
            force "androidx.recyclerview:recyclerview:$rootProject.ext.androidXRecyclerViewVersion"
            force "androidx.recyclerview:recyclerview-selection:$rootProject.ext.androidXVersion"
            force "com.google.android.material:material:$rootProject.ext.androidXVersion"
            force "androidx.cardview:cardview:$rootProject.ext.androidXVersion"
            force "androidx.vectordrawable:vectordrawable-animated:$rootProject.ext.androidXVectorDrawableVersion"
            force "androidx.multidex:multidex:$rootProject.ext.androidXMultidexVersion"
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            /*output.processManifest.doLast {
                def manifestOutFile = output.processManifest.manifestOutputFile
                def manifestContents = manifestOutFile.getText('UTF-8')
                def newFileContents = manifestContents.replaceAll("${debug}", rootProject.ext.debug)
                manifestOutFile.write(newFileContents, 'UTF-8')
            }*/

            if (variant.getBuildType().isMinifyEnabled()) {
                variant.getAssembleProvider().get().doLast {
                    def fileList = variant.mappingFile.parentFile.listFiles()
                    println("output:mappingfilepath:" + variant.mappingFile.path)
                    println("output:mappingfile.parentFile.listFiles:" + fileList)
                    println("output:mappingfile.exists:" + variant.mappingFile.exists())

                    if (!fileList.every { it.name == "mapping.txt" }) {
                        fileList.each {
                            if (it.name.contains("-mapping.txt")) {
                                println("output:copy from:${it.path}")
                                println("output:copy to:${it.parent}" + "/mapping.txt")
                                def fromFile = it
                                def toDir = it.parent
                                copy {
                                    println("output:copy from:${fromFile}")
                                    println("output:copy to:${toDir}" + "/mapping.txt")

                                    from fromFile
                                    into toDir
                                    rename { String fileName ->
                                        "mapping.txt"
                                    }
                                }
                                println("output:copy end")
                            }
                        }
                    }
                }
            }
        }
    }
}

/**
 * api          :   常规的打包配置，会将依赖代码打入到dex中去
 * bundleCompile    :   bundle中的代码不会打入dex中去，bundle将以libxxx.so文件的形式，放在apk的lib目录下，随包发布
 * outOfApkBundles  :   指定远程bundle，不会打入apk中
 */
dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])

    if (rootProject.ext.react.get("enableHermes", false)) {
        debugImplementation files(rootProject.ext.hermesPath + "hermes-debug.aar")
        releaseImplementation files(rootProject.ext.hermesPath + "hermes-release.aar")
    } else {
        implementation 'org.webkit:android-jsc:+'
    }


    api project(':library-template-base')
    api project(':library-template-repository')

    if (gradle.ext.modules.liveStreaming.enable == true) {
        api project(':library-livestreaming')
        api project(':library-livestreaming-push')
    }

    if (gradle.ext.modules.flutter.enable == true) {
        api project(':library-flutter')
    }

    if (gradle.ext.modules.hybird.enable == true) {
        api project(':library-hybird')
    }

    if (gradle.ext.modules.map.enable == true) {
        api project(':library-map')
    }

    if (gradle.ext.modules.reactNative.enable == true) {
        api project(':library-reactnative')
    }

    api project(':module-template-mine')

    // LeakCanary
    debugApi "com.squareup.leakcanary:leakcanary-android:$rootProject.ext.leakcanaryVersion"
    releaseApi "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.ext.leakcanaryVersion"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.ext.leakcanaryVersion"

    if (gradle.ext.configs.test.enable == true) {
        // Dependencies for local unit tests
        testImplementation "junit:junit:$rootProject.ext.junitVersion"
        testImplementation "org.mockito:mockito-all:$rootProject.ext.mockitoVersion"
        testImplementation "org.hamcrest:hamcrest-all:$rootProject.ext.hamcrestVersion"

        // Android Testing Support Library's runner and rules
        androidTestImplementation "androidx.test:runner:$rootProject.ext.runnerVersion"
        androidTestImplementation "androidx.test:rules:$rootProject.ext.rulesVersion"

        // Dependencies for Android unit tests
        androidTestImplementation "junit:junit:$rootProject.ext.junitVersion"
        androidTestImplementation "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"
        androidTestImplementation "com.google.dexmaker:dexmaker:$rootProject.ext.dexmakerVersion"
        androidTestImplementation "com.google.dexmaker:dexmaker-mockito:$rootProject.ext.dexmakerVersion"

        // Set this dependency to build and run UI Automator tests
        androidTestImplementation 'androidx.test.uiautomator:uiautomator-v18:1.0.0'

        // Espresso UI Testing
        androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.ext.espressoVersion"
        androidTestImplementation "androidx.test.espresso:espresso-contrib:$rootProject.ext.espressoVersion"
        androidTestImplementation "androidx.test.espresso:espresso-intents:$rootProject.ext.espressoVersion"
        androidTestImplementation "androidx.test.espresso:espresso-idling-resource:$rootProject.ext.espressoVersion"
        androidTestImplementation("androidx.test.espresso:espresso-core:$rootProject.ext.espressoVersion"/*, {
            exclude group: "com.google.code.findbugs"
        }*/)
    }
}

//apply from: 'tinker-support.gradle'
