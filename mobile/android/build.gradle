// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlinVersion = "1.3.71"
    ext.ankoVersion = "0.8.2"
    ext.walleVersion = "1.1.6"

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        maven {
            url("https://jitpack.io")
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.4'
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5" // https://github.com/bintray/gradle-bintray-plugin

        if (gradle.ext.configs.walle.enable == true) {
            classpath "com.meituan.android.walle:plugin:$walleVersion"
        }
        if (gradle.ext.configs.bugly.enable == true) {
            classpath "com.tencent.bugly:symtabfileuploader:1.3.9"
        }
        if (gradle.ext.configs.tinker.enable == true) {
            classpath "com.tencent.bugly:tinker-support:1.0.8"
        }

        if (gradle.ext.modules.videoplayer.enable == true) {
            // 为了使得 butterKnife 能在 library 中使用
            classpath 'com.jakewharton:butterknife-gradle-plugin:10.2.1'
        }
    }
}

allprojects {
    repositories {
        if (gradle.ext.modules.flutter.enable == true) {
            maven { url "$rootDir/app/libraries/repo/host/outputs/repo" }
            maven { url 'http://download.flutter.io' }
        }

        if (gradle.ext.modules.reactNative.enable == true) {
            if (gradle.ext.modules.reactNative.repositoryLocal.enable == true) {
                maven { url gradle.ext.modules.reactNative.repositoryLocal.url }
            } else {
                maven { url "${gradle.ext.modules.reactNative.nodeModulesPath}/react-native/android" } // All of React Native (JS, Android binaries) is installed from npm
                // react-native couldn't find DSO to load: libhermes.so
                maven { url("${gradle.ext.modules.reactNative.nodeModulesPath}/jsc-android/dist") } // Android JSC is installed from npm
            }
        }

        flatDir { dirs "file://$rootProject.projectDir/app/libraries/repo/aars" }

        maven { url "https://jitpack.io" }
        mavenLocal()
        google()
        jcenter()
        mavenCentral()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
    tasks.withType(Javadoc) {
        options.encoding = "UTF-8"
    }
}

task clean(type: Delete) {
    delete buildDir
}

apply from: "gradle/readme/scripts/tasktimings.gradle" //控制台输出任务耗时

// Define versions in a single place
// noinspection SpellCheckingInspection
ext {

    // Sdk and tools

    // react-native>=16
    // ijkplayer>=16
    // ijkplayer-arm64>=21
    // yasea>=16
    // yasea-arm64>=21
    minSdkVersion = 21

    // 设置为 26 如果适配Android 8.0 (targetSdkVersion = 26) 应用图标会出现适配问题, 如果已经做了适配操作,在谷歌 Pixel 2 图标显示正常, 但是在华为 Mate10 显示的却是圆形; 如果不适配则谷歌 Pixel 2 图标显示被一个框框包裹进去,
    //          所以目前设置为 targetSdkVersion = 25, 直接让 UI 给 5套图即可, 不做 android 8.0  Adaptive icons 适配
    // 设置为 27 会有Bug https://stackoverflow.com/questions/46980697/lock-screen-orientation-when-targeting-android-api-27-with-a-non-opaque-activity
    // https://dl.google.com/dl/android/maven2/index.html
    // https://developer.android.google.cn/studio/releases/gradle-plugin#updating-plugin
    // 设置为 29 报错 Media projections require a foreground service of type ServiceInfo.FOREGROUND_SERVICE_TYPE_MEDIA_PRO
    targetSdkVersion = 29
    compileSdkVersion = 29
    buildToolsVersion = "29.0.3" //"28.0.3"

    // App dependencies
    supportLibraryVersion = "28.0.0" // "28.0.0"
    constraintLayoutVersion = "2.0.0-beta3"

    // AndroidX
    androidXVersion = "1.0.0"
    androidXCoreVersion = "1.3.2"
    androidXMultidexVersion = "2.0.1"
    androidXAnnotationVersion = "1.1.0"
    androidXAppCompatVersion = "1.2.0"
    androidXRecyclerViewVersion = "1.1.0"
    androidXVectorDrawableVersion = "1.1.0"
    androidXFragmentVersion = "1.2.5"
    androidXConstraintlayoutVersion = "2.0.4"
    androidXMaterialVersion = "1.2.1"
    androidXLifecycleVersion = "2.3.0-beta01"

    // React Native Version
    reactNativeVersion = "0.62.2"

    smartRefreshLayoutVersion = "2.0.1"
    // guavaVersion = "24.1-jre"
    junitVersion = "4.12"
    mockitoVersion = "1.0.0"
    espressoVersion = "1.0.0"
    rxjavaVersion = "2.2.19"
    rxkotlinVersion = "2.4.0"
    rxandroidVersion = "2.1.1"
    rxbindingVersion = "2.0.0"

    //region react native dependency
    /*
    |    +--- com.smart.library:libs-reactnative:0.62.2.1
    |    |    +--- androidx.transition:transition:1.1.0 (*)
    |    |    +--- androidx.appcompat:appcompat:1.0.2 -> 1.2.0 (*)
    |    |    +--- javax.inject:javax.inject:1
    |    |    +--- com.google.code.findbugs:jsr305:3.0.2 -> 1.3.9
    |    |    +--- com.squareup.okhttp3:okhttp:3.12.1 -> 4.9.0 (*)
    |    |    +--- com.squareup.okhttp3:okhttp-urlconnection:3.12.1 -> 4.9.0 (*)
    |    |    +--- com.squareup.okio:okio:1.15.0 -> 1.14.0
    |    |    +--- com.facebook.infer.annotation:infer-annotation:0.11.2
    |    |    |    \--- com.google.code.findbugs:jsr305:3.0.1 -> 1.3.9
    |    |    +--- com.facebook.yoga:proguard-annotations:1.14.1
    |    |    +--- com.facebook.fresco:fresco:2.0.0
    |    |    |    +--- com.facebook.fresco:fbcore:2.0.0
    |    |    |    +--- com.facebook.fresco:drawee:2.0.0
    |    |    |    |    +--- com.facebook.fresco:fbcore:2.0.0
    |    |    |    |    \--- com.facebook.fresco:imagepipeline:2.0.0
    |    |    |    |         +--- com.facebook.fresco:imagepipeline-base:2.0.0
    |    |    |    |         |    +--- com.facebook.soloader:soloader:0.6.0 -> 0.8.0
    |    |    |    |         |    |    +--- com.facebook.soloader:annotation:0.8.0
    |    |    |    |         |    |    \--- com.facebook.soloader:nativeloader:0.8.0
    |    |    |    |         |    +--- com.parse.bolts:bolts-tasks:1.4.0
    |    |    |    |         |    \--- com.facebook.fresco:fbcore:2.0.0
    |    |    |    |         +--- com.facebook.soloader:soloader:0.6.0 -> 0.8.0 (*)
    |    |    |    |         +--- com.parse.bolts:bolts-tasks:1.4.0
    |    |    |    |         \--- com.facebook.fresco:fbcore:2.0.0
    |    |    |    +--- com.facebook.fresco:imagepipeline:2.0.0 (*)
    |    |    |    +--- com.facebook.fresco:nativeimagefilters:2.0.0
    |    |    |    |    +--- com.facebook.fresco:imagepipeline:2.0.0 (*)
    |    |    |    |    +--- com.facebook.soloader:soloader:0.6.0 -> 0.8.0 (*)
    |    |    |    |    +--- com.parse.bolts:bolts-tasks:1.4.0
    |    |    |    |    \--- com.facebook.fresco:fbcore:2.0.0
    |    |    |    +--- com.facebook.fresco:nativeimagetranscoder:2.0.0
    |    |    |    |    +--- com.facebook.fresco:imagepipeline-base:2.0.0 (*)
    |    |    |    |    +--- com.facebook.soloader:soloader:0.6.0 -> 0.8.0 (*)
    |    |    |    |    +--- com.parse.bolts:bolts-tasks:1.4.0
    |    |    |    |    \--- com.facebook.fresco:fbcore:2.0.0
    |    |    |    \--- com.facebook.soloader:soloader:0.6.0 -> 0.8.0 (*)
    |    |    +--- com.facebook.fresco:imagepipeline-okhttp3:2.0.0
    |    |    |    +--- com.squareup.okhttp3:okhttp:3.12.1 -> 4.9.0 (*)
    |    |    |    +--- com.facebook.fresco:fbcore:2.0.0
    |    |    |    \--- com.facebook.fresco:imagepipeline:2.0.0 (*)
    |    |    +--- com.facebook.soloader:soloader:0.8.0 (*)
    |    |    +--- com.facebook.fbjni:fbjni-java-only:0.0.3
    |    |    |    \--- com.facebook.soloader:nativeloader:0.8.0
    */
    frescoVersion = "1.12.1"
    okhttpVersion = "3.12.1"
    findBugsVersion = "3.0.1"
    /*
    |         +--- com.airbnb.android:lottie:3.2.2
    |         |    +--- androidx.appcompat:appcompat:1.0.0 -> 1.2.0 (*)
    |         |    \--- com.squareup.okio:okio:1.17.4 -> 1.15.0
    */
    okioVersion = "1.17.4"
    soLoaderVersion = "0.8.0"
    //endregion

    retrofitVersion = "2.9.0"
    leakcanaryVersion = "2.2"
    crashreportUpgradeVersion = "1.3.1"
    libraryBaseVersion = "0.0.1-SNAPSHOT"

    //others
    debug = false
    versionCode = 1
    versionName = "1.0"
    versionRN = 1           // react native base bundle version

    Properties properties = new Properties()
    try {
        properties.load(project.rootProject.file("local.properties").newDataInputStream())
        debug = "true".equalsIgnoreCase(properties.getProperty("app.debug"))
        versionCode = Integer.parseInt(properties.getProperty("app.versionCode", versionCode.toString()))

        print("debug and versionCode read success,debug=${debug} and versionCode=${versionCode}")
    } catch (Exception ignored) {
        print("debug and versionCode read failure,use default debug=${debug} and versionCode=${versionCode}")
    }
}
