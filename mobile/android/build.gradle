// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlinVersion = "1.3.70"
    ext.ankoVersion = "0.8.2"
    ext.godeyeVersion = "3.2.1"
    ext.godeyeMethodCanaryVersion = "0.15.2"
    ext.walleVersion = "1.1.6"

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        maven {
            url("https://jitpack.io")
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.1'
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"

        if (gradle.ext.configs.walle.enable == true) {
            classpath "com.meituan.android.walle:plugin:$walleVersion"
        }
        if (gradle.ext.configs.bugly.enable == true) {
            classpath "com.tencent.bugly:symtabfileuploader:1.3.9"
        }
        if (gradle.ext.configs.tinker.enable == true) {
            classpath "com.tencent.bugly:tinker-support:1.0.8"
        }

        // 为了使得 butterKnife 能在 library 中使用
        classpath 'com.jakewharton:butterknife-gradle-plugin:10.2.1'

        // https://github.com/Kyson/MethodCanary 监控方法耗时 第一处配置
        if (gradle.ext.configs.monitoring.godeye.enable == true && gradle.ext.configs.monitoring.godeye.enableMethodCanary == true) {
            classpath "cn.hikyson.methodcanary:plugin:0.15.2"
        }
    }
}

allprojects {
    repositories {
        flatDir {
            dirs "file://$rootProject.projectDir/app/libraries/repo/aars"
        }
        mavenLocal()
        maven {
            // All of React Native (JS, Android binaries) is installed from npm
            url "$rootDir/../../web/react_native/node_modules/react-native/android"
        }

        // react-native couldn't find DSO to load: libhermes.so
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../../web/react_native/node_modules/jsc-android/dist")
        }
        google()
        jcenter()
        mavenCentral()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        maven {
            url("https://jitpack.io")
        }
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
    tasks.withType(Javadoc) {
        options.encoding = "UTF-8"
    }
}

task clean(type: Delete) {
    //noinspection GroovyAssignabilityCheck
    delete buildDir
}

apply from: "gradle/readme/scripts/tasktimings.gradle" //控制台输出任务耗时

// Define versions in a single place
ext {

    react = [
            entryFile    : "index.js",
            // jsBundleDirRelease: "$buildDir/intermediates/merged_assets/release/out",
            enableHermes : false,  // clean and rebuild if changing
            hermesCommand: "$rootDir/../../web/react_native/node_modules/hermes-engine/%OS-BIN%/hermes",
    ]
    hermesPath = "$rootDir/../../web/react_native/node_modules/hermes-engine/android/"

    // Sdk and tools

    // react-native>=16
    // ijkplayer>=16
    // ijkplayer-arm64>=21
    // yasea>=16
    // yasea-arm64>=21
    minSdkVersion = 19

    // 设置为 26 如果适配Android 8.0 (targetSdkVersion = 26) 应用图标会出现适配问题, 如果已经做了适配操作,在谷歌 Pixel 2 图标显示正常, 但是在华为 Mate10 显示的却是圆形; 如果不适配则谷歌 Pixel 2 图标显示被一个框框包裹进去,
    //          所以目前设置为 targetSdkVersion = 25, 直接让 UI 给 5套图即可, 不做 android 8.0  Adaptive icons 适配
    // 设置为 27 会有Bug https://stackoverflow.com/questions/46980697/lock-screen-orientation-when-targeting-android-api-27-with-a-non-opaque-activity
    // https://dl.google.com/dl/android/maven2/index.html
    // https://developer.android.google.cn/studio/releases/gradle-plugin#updating-plugin
    targetSdkVersion = 29
    compileSdkVersion = 29
    buildToolsVersion = "29.0.3" //"28.0.3"

    // App dependencies
    supportLibraryVersion = "28.0.0" // "28.0.0"
    constraintLayoutVersion = "2.0.0-beta3"

    // AndroidX
    androidXVersion = "1.0.0"
    androidXCoreVersion = "1.2.0"
    androidXMultidexVersion = "2.0.1"
    androidXAnnotationVersion = "1.1.0"
    androidXAppCompatVersion = "1.1.0"
    androidXRecyclerViewVersion = "1.1.0"
    androidXVectorDrawableVersion = "1.1.0"
    androidXFragmentVersion = "1.2.2"
    androidXConstraintlayoutVersion = "1.1.3"
    androidXMaterialVersion = "1.1.0"
    androidXLifecycleVersion = "2.2.0"

    // React Native Version
    reactNativeVersion = "0.61.5"

    smartRefreshLayoutVersion = "2.0.0-alpha-1"
    guavaVersion = "22.0-android"
    junitVersion = "4.12"
    mockitoVersion = "1.0.0"
    espressoVersion = "1.0.0"
    rxjavaVersion = "2.1.0"
    rxkotlinVersion = "2.0.3"
    rxandroidVersion = "2.0.1"
    rxlifecycleVersion = "2.0.1"
    rxbindingVersion = "2.0.0"
    frescoVersion = "1.12.1"
    okhttpVersion = "3.13.1"
    retrofitVersion = "2.5.0"
    leakcanaryVersion = "2.2"
    crashreportUpgradeVersion = "1.3.1"
    libraryBaseVersion = "0.0.1-SNAPSHOT"

    //others
    debug = false
    versionCode = 1
    versionName = "1.0"
    versionRN = 1           // react native base bundle version

    Properties properties = new Properties()
    try {
        properties.load(project.rootProject.file("local.properties").newDataInputStream())
        debug = "true".equalsIgnoreCase(properties.getProperty("app.debug"))
        versionCode = Integer.parseInt(properties.getProperty("app.versionCode", versionCode.toString()))

        print("debug and versionCode read success,debug=${debug} and versionCode=${versionCode}")
    } catch (Exception e) {
        e.printStackTrace()
        print("debug and versionCode read failure,use default debug=${debug} and versionCode=${versionCode}")
    }
}
