// Log timings per task.
class TimingsListener implements TaskExecutionListener, BuildListener {
    private long timeStart = 0
    private timings = []

    @Override
    void beforeExecute(Task task) {
        timeStart = System.currentTimeMillis()
    }

    @Override
    void afterExecute(Task task, TaskState taskState) {
        def ms = System.currentTimeMillis() - timeStart
        timings.add([ms, task.path])
        task.project.logger.warn "${task.path} spent ${ms}ms"
    }

    @Override
    void buildFinished(BuildResult result) {
        println "Task timings:"
        for (timing in timings)
            if (timing[0] >= 50)
                printf "%7sms  %s\n", timing
    }

    @Override
    void buildStarted(Gradle gradle) {}

    @Override
    void projectsEvaluated(Gradle gradle) {}

    @Override
    void projectsLoaded(Gradle gradle) {}

    @Override
    void settingsEvaluated(Settings settings) {}
}

gradle.addListener new TimingsListener()
