apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        applicationId "com.smart.flutter.template"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            /**
             * https://developer.android.com/ndk/guides/abis?hl=zh-cn
             * Android 系统运行时 会优先寻找 主要ABI(主要ABI可以实现系统最佳性能), 没有的话会选择 辅助ABI, 如果找都找不到, 应用可以安装但会在运行时奔溃
             * --------------------------------------------------------------------------------
             * cpu arch                 primary-abi             secondary-abi
             * --------------------------------------------------------------------------------
             * armeabi                  armeabi                 none
             * armeabi-v7a              armeabi-v7a             armeabi
             * arm64-v8a                arm64-v8a               armeabi-v7a / armeabi
             * --------------------------------------------------------------------------------
             * x86                      x86                     armeabi-v7a / armeabi
             * x86_64                   x86_64                  x86_64 / x86 / armeabi
             * --------------------------------------------------------------------------------
             * mips                     mips                    none
             * mips64                   mips64                  mips
             * --------------------------------------------------------------------------------
             */
            /**
             *  flutter support 'armeabi-v7a', 'arm64-v8a', not support 'armeabi', 'x86', 'x86_64'
             */
            abiFilters 'armeabi-v7a'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            // TODO: Add your own signing config for the release build.
            // Signing with the debug keys for now, so `flutter run --release` works.
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        main {
            java {
                srcDirs 'src/main/java'
                exclude 'io/flutter/plugins/**'
            }
        }
    }

}

afterEvaluate {
    preBuild.dependsOn ':flutter-bundle:assembleDebug' // app 编译之前 确保重新编译并更新 flutter-bundle aar
    preBuild.dependsOn ':flutter-bundle:assembleRelease'
}

dependencies {
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'

    if (rootProject.ext.inbuiltThirdLibraries) {
        api(name: "flutter-bundle-1.0.0-debug", ext: "aar")
    } else {
        api project(":flutter-bundle")
    }
}
