def finalName= 'spingcloud-routing-zuul'
def finalPort = "5389"

apply plugin: "com.google.cloud.tools.jib"
jib {
    from {
        image = 'java:8'
    }
    to {
        image = "docker.io/krmao/${finalName}"  // 上传后在 https://hub.docker.com/repositories 中查看
        auth {
            username = rootProject.ext.docker_io_user
            password = rootProject.ext.docker_io_password
        }
    }
    container {
        creationTime = 'USE_CURRENT_TIMESTAMP'
        jvmFlags = ['-Xms512m', '-Xdebug', '-Dfile.encoding=utf-8', '-Duser.timezone=Asia/Shanghai']
        ports = [finalPort]
    }
    dockerClient {
        executable = "/usr/local/bin/docker"
    }
}

version = rootProject.ext.spingcloud_routing_zuul_version

//region 将 local.properties 参数读取到 rootProject.ext, 在此绑定到 application.yml
// https://docs.spring.io/spring-boot/docs/1.4.x/reference/html/howto-properties-and-configuration.html
// https://github.com/spring-projects/spring-boot/commit/c0c67f2593dbfd17aa304b43f4da3a3678fa58eb
processResources {
    inputs.file("${rootProject.projectDir}/local.properties")
    filesMatching("application.yml") {
        logger.error(">>>>>>>>>>==============================>>>>>>>>>>")
        logger.error("\n[processResources](filesMatched) ${it.path}\n")
        logger.error("<<<<<<<<<<==============================<<<<<<<<<<")
        expand(rootProject.properties)
    }
}
//endregion

dependencies {
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-zuul'
}
